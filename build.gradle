plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'io.github.inshakr2'
version = '1.0.0'

ext {
    slackBoltVersion = '1.45.3'
    springBootVersion = '2.7.18'
    publishableModules = ['slack-bolt-socket-mode-core', 'slack-bolt-socket-mode-spring-boot-starter'] as Set
    releasePropertyNames = ['sonatypeUsername', 'sonatypePassword', 'signingKey', 'signingPassword']
    normalizePgpSecretKey = { String rawKey ->
        if (rawKey == null) {
            return null
        }
        String normalized = rawKey
                .replace('\r\n', '\n')
                .replace('\\n', '\n')
                .trim()
        if (normalized.isEmpty()) {
            return null
        }
        if (!normalized.endsWith('\n')) {
            normalized = normalized + '\n'
        }
        return normalized
    }
}

tasks.register('validateReleaseCredentials') {
    group = 'publishing'
    description = 'Validates required Sonatype/GPG properties for release publishing.'
    doLast {
        def signingSkip = providers.gradleProperty('signing.skip').map { it.toBoolean() }.orElse(false).get()
        if (signingSkip) {
            logger.lifecycle('Skipping release credential validation because signing.skip=true')
            return
        }

        def missing = rootProject.ext.releasePropertyNames.findAll { key ->
            !providers.gradleProperty(key).isPresent() || providers.gradleProperty(key).get().trim().isEmpty()
        }
        if (!missing.isEmpty()) {
            throw new GradleException("Missing required Gradle properties for release: ${missing.join(', ')}")
        }

        def normalizedSigningKey = rootProject.ext.normalizePgpSecretKey(providers.gradleProperty('signingKey').orNull)
        if (normalizedSigningKey == null) {
            throw new GradleException('signingKey must not be blank')
        }
        if (normalizedSigningKey.contains('BEGIN PGP PUBLIC KEY BLOCK')) {
            throw new GradleException('signingKey appears to be a public key. Use: gpg --armor --export-secret-keys <KEY_ID>')
        }
        if (!normalizedSigningKey.contains('BEGIN PGP PRIVATE KEY BLOCK')
                || !normalizedSigningKey.contains('END PGP PRIVATE KEY BLOCK')) {
            throw new GradleException('signingKey must contain an ASCII-armored private key block')
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://ossrh-staging-api.central.sonatype.com/service/local/')
            snapshotRepositoryUrl = uri('https://central.sonatype.com/repository/maven-snapshots/')
            username = providers.gradleProperty('sonatypeUsername').orNull
            password = providers.gradleProperty('sonatypePassword').orNull
        }
    }
}

tasks.matching { it.name in ['publishToSonatype', 'closeAndReleaseSonatypeStagingRepository'] }.configureEach {
    dependsOn(tasks.named('validateReleaseCredentials'))
}

subprojects {
    apply plugin: 'java-library'

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = 11
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.10.2')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.assertj:assertj-core:3.25.3'
        testImplementation 'org.mockito:mockito-core:5.12.0'
        testImplementation 'org.mockito:mockito-inline:5.2.0'
    }

    if (project.name in rootProject.ext.publishableModules) {
        apply plugin: 'maven-publish'
        apply plugin: 'signing'

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    groupId = project.group
                    artifactId = project.name
                    version = project.version

                    pom {
                        name = project.name
                        description = 'Slack Bolt Socket Mode handlers and Spring Boot starter'
                        url = 'https://github.com/inshakr2/slack-bolt-socket-mode-spring-boot-starter'

                        licenses {
                            license {
                                name = 'Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0'
                            }
                        }

                        developers {
                            developer {
                                id = 'inshakr2'
                                name = 'inshakr2'
                            }
                        }

                        scm {
                            connection = 'scm:git:https://github.com/inshakr2/slack-bolt-socket-mode-spring-boot-starter.git'
                            developerConnection = 'scm:git:https://github.com/inshakr2/slack-bolt-socket-mode-spring-boot-starter.git'
                            url = 'https://github.com/inshakr2/slack-bolt-socket-mode-spring-boot-starter'
                        }
                    }
                }
            }

            repositories {
                mavenLocal()
            }
        }

        signing {
            def signingSkip = providers.gradleProperty('signing.skip').map { it.toBoolean() }.orElse(false).get()
            required {
                !signingSkip && gradle.taskGraph.allTasks.any { task ->
                    task.name.startsWith('publish') || task.name.contains('Sonatype')
                }
            }
            if (!signingSkip) {
                def key = rootProject.ext.normalizePgpSecretKey(providers.gradleProperty('signingKey').orNull)
                def password = providers.gradleProperty('signingPassword').orNull
                if (key != null && password != null) {
                    useInMemoryPgpKeys(key, password)
                }
                sign(publishing.publications.mavenJava)
            }
        }
    }
}
