group = 'io.github.inshakr2'
version = '0.1.0-SNAPSHOT'

ext {
    slackBoltVersion = '1.45.3'
    springBootVersion = '2.7.18'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = 11
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.10.2')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.assertj:assertj-core:3.25.3'
        testImplementation 'org.mockito:mockito-core:5.12.0'
        testImplementation 'org.mockito:mockito-inline:5.2.0'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId = project.group
                artifactId = project.name
                version = project.version

                pom {
                    name = project.name
                    description = 'Safe Slack Bolt handlers and Spring Boot starter'
                    url = 'https://github.com/inshakr2/safe-slack-bolt-spring-starter'

                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    developers {
                        developer {
                            id = 'inshakr2'
                            name = 'inshakr2'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/inshakr2/safe-slack-bolt-spring-starter.git'
                        developerConnection = 'scm:git:https://github.com/inshakr2/safe-slack-bolt-spring-starter.git'
                        url = 'https://github.com/inshakr2/safe-slack-bolt-spring-starter'
                    }
                }
            }
        }

        repositories {
            mavenLocal()
        }
    }

    signing {
        def signingSkip = providers.gradleProperty('signing.skip').map { it.toBoolean() }.orElse(false).get()
        required { !signingSkip && gradle.taskGraph.allTasks.any { it.name.startsWith('publish') } }
        if (!signingSkip) {
            sign publishing.publications.mavenJava
        }
    }
}
